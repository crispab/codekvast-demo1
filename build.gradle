buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

version = '1.0.0'
sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

//--- Changes to make it work with Codekvast

configurations {
    codekvastagent
}

dependencies {
    codekvastagent 'io.codekvast:codekvast-agent:0.20.0:all'
}

apply plugin: 'application'
bootRepackage {
    enabled = false
}

def codekvastAgent=configurations.codekvastagent.asPath
applicationDefaultJvmArgs = [
        "-Xbootclasspath/a:$codekvastAgent",
        "-javaagent:$codekvastAgent"
]

// Make Heroku do the right thing instead of the standard Spring Boot buildpack stuff...
task stage(dependsOn: ['clean', 'build', 'installDist'])
build.mustRunAfter clean